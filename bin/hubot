#!/usr/bin/env coffee
##
# hubot [options]
#
# Launch an interactive hubot
#
require.paths.unshift __dirname + "/../node_modules"
require.paths.unshift __dirname + "/../src"

Path       = require 'path'
HTTP       = require 'http'
OptParse   = require 'optparse'
PortNumber = parseInt(process.env.PORT) || 8080

Switches = [
  [ "-h", "--help",              "Display the help information"],
  [ "-a", "--adapter ADAPTER",   "The Adapter to use"]
]

Options =
  adapter: "stdio"

Parser = new OptParse.OptionParser(Switches)
Parser.banner = "Usage hubot [options]"

Parser.on "adapter", (opt, value) ->
  Options.adapter = value

Parser.parse process.ARGV

switch Options.adapter
  when "irc"
    Hubot = require("hubot/irc").IrcBot
  when "campfire"
    Hubot = require("hubot/campfire").Campfire
  else
    Hubot = require("hubot/shell").Shell

robot = new Hubot Path.resolve(__dirname, "..", "scripts")
robot.run()

if webHost = process.env.HUBOT_WEB_HOST
  server = HTTP.createServer( (req, res) ->
    res.writeHead 200, 'Content-Type': 'text/plain'
    res.end 'Hello from Hubot\n'
  ).listen PortNumber

  setInterval ->
    httpOpts = host: webHost

    HTTP.get( httpOpts, (res) ->
      console.log "Got response: #{res}" unless res.statusCode == 200
    ).on 'error', (e) ->
      console.log "Got error: #{e.message}"
  , 60000

# vim:ft=coffee
